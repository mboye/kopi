** Settings **
Library     OperatingSystem
Library     Process
Library     String
Library     Collections

** Variables **
${indexer bin}      bin/kopi-index
${relative path}    test/resources/index
${absolute path}    ${CURDIR}/resources/index

** Test Cases **
Create index from relative path
    ${lines}=   Create index from ${relative path}
    Length should be    ${lines}    4

    ${line}=                Get from list   ${lines}  0
    Should match regexp     ${line}    {"path":"test/resources/index","size":0,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  1
    Should match regexp     ${line}    {"path":"test/resources/index/file-a.txt","size":10,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  2
    Should match regexp     ${line}    {"path":"test/resources/index/subdir","size":0,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  3
    Should match regexp     ${line}    {"path":"test/resources/index/subdir/file-b.txt","size":10,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

Create index from absolute path
    ${lines}=   Create index from ${absolute path}
    Length should be    ${lines}    4

    ${line}=                Get from list   ${lines}  0
    Should match regexp     ${line}    {"path":"${CURDIR}/resources/index","size":0,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  1
    Should match regexp     ${line}    {"path":"${CURDIR}/resources/index/file-a.txt","size":10,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  2
    Should match regexp     ${line}    {"path":"${CURDIR}/resources/index/subdir","size":0,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  3
    Should match regexp     ${line}    {"path":"${CURDIR}/resources/index/subdir/file-b.txt","size":10,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

Create index without recursing
    ${result}=  Run process  ${indexer bin}  --recursive\=false  ${relative path}
    Should be equal as integers  ${result.rc}  0
    ${lines}=   Split to lines  ${result.stdout}
    Length should be    ${lines}    2

    ${line}=                Get from list   ${lines}  0
    Should match regexp     ${line}    {"path":"test/resources/index","size":0,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

    ${line}=                Get from list   ${lines}  1
    Should match regexp     ${line}    {"path":"test/resources/index/file-a.txt","size":10,"modifiedTime":"\\d{4}-\\d{2}-\\d{2}T\\d{2}:\\d{2}:\\d{2}\.\\d+Z","mode":\\d+

Create index from missing path
    ${result}=  Run process  ${indexer bin}  /tmp/missing/path
    Should be equal as integers  ${result.rc}  1
    Should contain  ${result.stderr}  Failed to walk path: /tmp/missing/path
    Should contain  ${result.stderr}  no such file or directory

** Keywords **
Create index from ${path}
    ${result}=  Run process  ${indexer bin}  ${path}
    Should be equal as integers  ${result.rc}  0
    ${lines}=   Split to lines  ${result.stdout}
    [Return]   ${lines}


